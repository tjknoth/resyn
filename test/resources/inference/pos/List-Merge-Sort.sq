data Pair a where
  Pair :: a -> a -> Pair a

data List a <q :: Int> where
  Nil  :: List a <q>
  Cons :: x:a -> xs: List {a | | q} <q> -> List a <q>

data Tree a <q :: Int> where
  Leaf :: x:a -> Tree a <q>
  Node :: l: { Tree {a | | q} <q> | | q}
       -> r: Tree {a | | q} <q> 
       -> Tree a <q>

split :: List {a | | 1} <{0}> -[1]-> Pair (List a <{0}>)
split = \xs. 
  match xs with
    Nil       -> Pair xs xs
    Cons y ys -> match split ys with
                   Pair ls rs -> Pair rs (Cons y ls)

build :: xs: List {a | | 4} <{0}> -[1]-> Tree {a | | 1} <{-1}>
build = \xs. 
  match xs with
    Nil       -> error
    Cons y ys -> 
      match ys with
        Nil       -> Leaf y
        Cons z zs -> 
          match split (Cons y (Cons z zs)) with
            Pair l r -> Node (build l) (build r)

merge :: List {a | | 1} <{0}> -> List {a | | 1} <{0}> -[1]-> List a <{0}>
merge = \xs. \ys. 
  match xs with
    Nil        -> ys
    Cons xh xt -> match ys with
                    Nil        -> xs
                    Cons yh yt -> if xh < yh
                                    then Cons xh (merge xt (Cons yh yt))
                                    else Cons yh (merge (Cons xh xt) yt)

--takes the contents of a tree and arranges them in a sorted list
flatten :: Tree {a | | 1} <{2}> -[1]-> List a <{0}>
flatten = \t. 
  match t with
    Leaf x -> Cons x Nil
    Node l r -> merge (flatten l) (flatten r)
